plugins {
    id 'org.springframework.boot' version '2.4.0'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'com.google.cloud.tools.jib' version '2.6.0'
    id 'java'
    id 'eclipse'
}

group = 'io.github.lameducks'
version = '0.1.0'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.immutables:value:2.8.8'
    compileOnly 'org.immutables:value-annotations:2.8.8'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

task createFactoryPath {
    doLast {
        println "Creating .factorypath..."
        file('.factorypath').withWriter {
            def builder = new groovy.xml.MarkupBuilder(it)
            builder.setDoubleQuotes(true)
            builder.'factorypath' {
                project.configurations.annotationProcessor.each { annotationProcessor->
                    factorypathentry(
                        kind: 'EXTJAR',
                        id: annotationProcessor.absolutePath,
                        enabled: true,
                        runInBatchMode: false
                    )
                }
            }
        }
    }
}

eclipse {
    synchronizationTasks createFactoryPath
}
